if (is_app) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion lib_android.compileSdkVersion
    buildToolsVersion lib_android.buildToolsVersion

    defaultConfig {
        minSdkVersion lib_android.minSdkVersion
        targetSdkVersion lib_android.targetSdkVersion
        versionCode lib_android.versionCode
        versionName lib_android.versionName

        testInstrumentationRunner lib_android.testInstrumentationRunner
        consumerProguardFiles lib_android.consumerProguardFiles
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = 1.8
    }

    sourceSets {
        main {
            if (is_app) {
                manifest.srcFile('src/main/java/debug/AndroidManifest.xml')
            } else {
                manifest.srcFile('src/main/AndroidManifest.xml')
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

}

dependencies {
    lib_dependencies.each { key, value ->
        implementation value
    }

    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    annotationProcessor 'com.alibaba:arouter-compiler:1.2.2'
    implementation project(path: ":common")
}
